buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.form-com.diff-coverage-gradle:diff-coverage:0.7.2'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
}

ext{
    libVersion = '3.2.0'
    libPackage = 'com.sgnatiuk'
    libName = 'kombi'
}

group libPackage
version libVersion

sourceCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

apply plugin: 'com.form.diff-coverage'
diffCoverageReport {
    def diffBase = project.hasProperty('diffBase') ? project.diffBase : 'HEAD'

    diffSource.git.compareWith diffBase
    jacocoExecFiles = files(jacocoTestReport.executionData)
    classesDirs = files(jacocoTestReport.classDirectories)
    srcDirs = files(jacocoTestReport.sourceDirectories)

    reports {
        html = true
        baseReportDir = project.buildDir.absolutePath
    }

    violationRules {
        minBranches = 0.9
        minLines = 0.9
        minInstructions = 0.9
        failOnViolation = true
    }
}

check.dependsOn diffCoverage
diffCoverage.dependsOn test
